plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom' version '1.0.+' apply false
    id 'com.matthewprenger.cursegradle' version "1.4.0"
}

class Globals {
    static def baseVersion = "2.1.0"
    static def mcVersion = "1.20.2-rc2"
    static def yarnVersion = "+build.1"
}

archivesBaseName = "Server-Translations"

allprojects {
    apply plugin: 'fabric-loom'

    version = Globals.baseVersion + "+" + Globals.mcVersion
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    group = "xyz.nucleoid"

    repositories {
        mavenCentral()
        maven { url = 'https://maven.nucleoid.xyz/' }
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'mojang'
            url = 'https://libraries.minecraft.net/'
        }
        maven {
            name = "Jitpack"
            url = "https://jitpack.io"
        }
        //mavenLocal()
    }

    dependencies {
        minecraft "com.mojang:minecraft:$Globals.mcVersion"
        mappings "net.fabricmc:yarn:${Globals.mcVersion}${Globals.yarnVersion}:v2"
        modImplementation "net.fabricmc:fabric-loader:0.14.22"

        modImplementation include("xyz.nucleoid:packet-tweaker:0.5.0+1.20.2-rc1")

        modImplementation "net.fabricmc.fabric-api:fabric-api:0.88.5+1.20.2"
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
        options.release.set(17)
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.archivesBaseName}"}
        }
    }

    if (it.name != "api") remapJar.dependsOn ':api:remapJar'
}

dependencies {
    implementation project(path: ':api', configuration: "namedElements")
    include project(':api'/*name: 'server-translations-api', version: project(':api').version, ext: 'jar'*/)
}
