plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom' version '1.11.+' apply false
    id 'com.matthewprenger.cursegradle' version "1.4.0"
}

class Globals {
    static def baseVersion = "c"
    static def mcVersion = "1.21.9-pre2"
    static def yarnVersion = "+build.2"
    static def apiVersion = "0.133.8+1.21.9"
}

archivesBaseName = "Server-Translations"

allprojects {
    apply plugin: 'fabric-loom'

    version = Globals.baseVersion + "+" + Globals.mcVersion
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    group = "xyz.nucleoid"

    repositories {
        mavenCentral()
        maven { url = 'https://maven.nucleoid.xyz/' }
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'mojang'
            url = 'https://libraries.minecraft.net/'
        }
        maven {
            name = "Jitpack"
            url = "https://jitpack.io"
        }
        //mavenLocal()
    }

    dependencies {
        minecraft "com.mojang:minecraft:$Globals.mcVersion"
        mappings "net.fabricmc:yarn:${Globals.mcVersion}${Globals.yarnVersion}:v2"
        modImplementation "net.fabricmc:fabric-loader:0.17.2"

        modImplementation include("xyz.nucleoid:packet-tweaker:0.6.0-pre.1+1.21.2-pre3")

        modCompileOnly modLocalRuntime("net.fabricmc.fabric-api:fabric-api:${Globals.apiVersion}")
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
        options.release.set(21)
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.archivesBaseName}"}
        }
    }

    if (it.name != "api") remapJar.dependsOn ':api:remapJar'
}

dependencies {
    implementation project(path: ':api', configuration: "namedElements")
    include project(':api'/*name: 'server-translations-api', version: project(':api').version, ext: 'jar'*/)
}
